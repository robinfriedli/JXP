plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "com.google.guava:guava:25.1-jre"
    implementation "org.seleniumhq.selenium:selenium-support:3.14.0"
    implementation "org.slf4j:slf4j-api:1.7.26"
    implementation "net.robinfriedli:StringList:1.6"

    testCompile "org.testng:testng:7.1.0"
    testCompile "com.google.truth:truth:1.0.1"
}

group = "net.robinfriedli"
version = "2.0.1"
description = "JXP"
sourceCompatibility = "8"
targetCompatibility = "8"

compileJava.options.encoding = "UTF-8"

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

test {
    useTestNG()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            artifactId "JXP"

            from(components.java)

            artifact tasks.sourceJar
        }
    }
}

def bintrayProperties = new Properties()
def bintrayPropertiesFile = file("src/main/resources/bintray.properties")
if (bintrayPropertiesFile.exists()) {
    bintrayPropertiesFile.withInputStream { bintrayProperties.load(it) }
} else {
    bintrayProperties.setProperty("bintray.user", "empty")
    bintrayProperties.setProperty("bintray.key", "empty")
}

bintray {
    user = bintrayProperties.get("bintray.user")
    key = bintrayProperties.get("bintray.key")
    publications = ['maven']
    pkg {
        repo = rootProject.group
        name = "JXP"
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}